<PROGRAM> ::= <DECLS>

/* declarations */
<DECLS> ::= <DECL> | <DECLS> <DECL>
<DECL> ::= <FN_DECL> | <VAR_DECL_STMT> ";" | <STRUCT_DECL_STMT> ";" 

/* function declaration */
<FN_DECL> ::= "fx" <IDENT> <PARAMS> "==>" <RETURN_TYPE> <STMT_BLOCK>

/* struct declaration */
<STRUCT_DECL_STMT> ::= "struct" <IDENT> <VAR_DECL_STMT_BLOCk>
<VAR_DECL_STMT_BLOCk> ::= "{" <VAR_DECL_STMTS> ";" "}"
<VAR_DECL_STMTS> ::= <VAR_DECL_STMT> | <VAR_DECL_STMTS> ";" <VAR_DECL_STMT>

/* statement block */
<STMT_BLOCK> ::= "{" "}" | "{" <STMTS> "}"

/* statements */
<STMTS> ::= <STMT> | <STMTS> <STMT> 

/* statement */
<STMT> ::= <STATEMENT_W_DELIM> ";" | <STATEMENT_WO_DELIM> 

/* statement types by delimiter */
<STATEMENT_W_DELIM> ::= <IN_IO_STMT> | <OUT_IO_STMT> | <VAR_DECL_STMT> | <VAR_DECL_INIT_STMT> | <VAR_ASSIGN_STMT> | <EXPR_STMT> | <BREAK_STMT> | <CONTINUE_STMT> | <RETURN_STMT>
<STATEMENT_WO_DELIM> ::= <IF_STMT> | <WHILE_STMT> | <FOR_STMT>

/* statement parts */
<IN_IO_STMT> ::= "in" "(" <IN_ARG_SEQ> ")"
<OUT_IO_STMT> ::= "disp" "(" <EXPR_SEQ> ")" 
<VAR_DECL_STMT> ::= <VAR_DECL>  
<VAR_DECL_INIT_STMT> ::= <VAR_DECL_INIT> 
<VAR_ASSIGN_STMT> ::= <VAR_ASSIGN> 
<EXPR_STMT> ::= <EXPR> 
<IF_STMT> ::= <IF_IF> <IF_ELIFS> <IF_ELSE> | <IF_IF> <IF_ELSE> | <IF_IF> <IF_ELIFS> 
<WHILE_STMT> ::= "while" "(" <EXPR> ")" <STMT_BLOCK>
<FOR_STMT> ::= "for" "(" <FOR_INIT> ";" <FOR_CONDITION> ";" <FOR_INCREMENT> ")" <STMT_BLOCK>
<BREAK_STMT> ::= "break" 
<CONTINUE_STMT> ::= "continue" 
<RETURN_STMT> ::= "return" | "return" <EXPR> 

/* in-statement parts */
<IN_ARG_SEQ> ::= <EXPR_SEQ> <IN_VAR_SEQ>
<IN_VAR_SEQ> ::= "&" <VAR> | <IN_VAR_SEQ> "," "&" <VAR>

/* if-statement parts */
<IF_IF> ::= "if" "("<EXPR>")" <STMT_BLOCK>
<IF_ELIFS> ::= <IF_ELIF> | <IF_ELIFS> <IF_ELIF> 
<IF_ELIF> ::= "elif" "(" <EXPR> ")" <STMT_BLOCK>
<IF_ELSE> ::= "else" <STMT_BLOCK> 

/* for-statement parts */
<FOR_INIT> ::= <VAR_DECL> | <VAR_DECL_INIT> | <VAR_ASSIGN> | <EXPR> | ""
<FOR_CONDITION> ::= <VAR_ASSIGN> | <EXPR> | ""
<FOR_INCREMENT> ::= <VAR_ASSIGN> | <EXPR> | ""

/* variable assignment */
<VAR_ASSIGN> ::= <VAR> "=" <EXPR>

/* variable initialization */
<VAR_DECL_INIT> ::= <TYPE_NAME> <IDENT> "=" <EXPR>

/* variable declaration */
<VAR_DECL> ::= <TYPE_NAME> <IDENT>

/* expressions */
<EXPR_SEQ> ::= <EXPR> | <EXPR_SEQ> "," <EXPR>

/* expression */
<EXPR> ::= <OR>
<OR> ::= <AND> | <OR> <OP_OR> <AND>
<AND> ::= <CMP_EQ_NEQ> | <AND> <OP_AND> <CMP_EQ_NEQ>
<CMP_EQ_NEQ> ::= <CMP_GT_LT> | <CMP_EQ_NEQ> <OP_CMP_EQ_NEQ> <CMP_GT_LT>
<CMP_GT_LT> ::= <SUM_SUB> | <CMP_GT_LT> <OP_CMP_GT_LT> <SUM_SUB>
<SUM_SUB> ::= <MUL_DIV_MOD> | <SUM_SUB> <OP_SUM_SUB> <MUL_DIV_MOD>
<MUL_DIV_MOD> ::= <UNARY> | <MUL_DIV_MOD> <OP_MUL_DIV_MOD> <UNARY>
<UNARY> ::= <PRIMARY> | <OP_UNARY> <UNARY>
<PRIMARY> ::= <TYPE_MEMBER> | <VAR> | <FN_CALL> | "(" <EXPR> ")"

/* function call */
<FN_CALL> ::= <IDENT> <ARGS>

/* arguments */
<ARGS> ::= "(" ")" | "(" <ARGS_SEQ> ")"
<ARGS_SEQ> ::= <ARG> | <ARGS_SEQ> "," <ARG>
<ARG> ::= <EXPR>

/* parameters */
<PARAMS> ::= "(" ")" | "(" <PARAMS_SEQ> ")"
<PARAMS_SEQ> ::= <PARAM> | <PARAMS_SEQ> "," <PARAM>
<PARAM> ::= <TYPE_NAME> <PARAM_IDENT>
<PARAM_IDENT> ::= <IDENT>

/* variables */
<VAR_SEQ> ::= <VAR> | <VAR_SEQ> "," < VAR>
<VAR> ::= <IDENT>

/* identifiers */
<IDENT> ::= <IDENT_HEAD_SYM> | <IDENT> <IDENT_TAIL_SYM>

/* return type */
<RETURN_TYPE> ::= <TYPE_NAME> | "void"

/* basic type members */
<TYPE_MEMBER> ::= <INT> | <FLOAT> | <BOOL> | <CHAR> | <STRING>

/* basic type names */
<TYPE_NAME> ::= "int" | "float" | "bool" | "char" | "string" 

/* basic types */
<STRING> ::= "\"" "\"" | "\"" <ANY> "\""
<CHAR> ::= "'" <ANY_SYM> "'" 
<BOOL> ::= "True" | "False"
<FLOAT> ::= <FLOAT_WO_E> | <FLOAT_WO_E> "e" <SIGNABLE_DIGITS> | <SIGNABLE_DIGITS> "e" <SIGNABLE_DIGITS>
<FLOAT_WO_E> ::= "." <DIGITS> | <SIGNABLE_DIGITS> "." |  <SIGNABLE_DIGITS> "." <DIGITS>
<INT> ::= <SIGNABLE_DIGITS>

/* unary operators (op level0) */
<OP_UNARY> ::= "!" | "++" | "--" 

/* arithmetic operators (op level1) */
<OP_MUL_DIV_MOD> ::= "*" | "/" | "%"

/* arithmetic operators (op level2) */
<OP_SUM_SUB> ::= "+" | "-"

/* cmp operators (op level3) */
<OP_CMP_GT_LT> ::= ">" | "<" | ">=" |"<="

/* cmp operators (op level4) */
<OP_CMP_EQ_NEQ> ::= "==" | "!="

/* logical operators (op level5) */
<OP_AND> ::= "AND"

/* logical operators (op level6) */
<OP_OR> ::= "OR"

/* assign operators */
<OP_ASSIGN> ::= "=" | "+=" | "-=" | "*=" | "/=" | "%="

/* comments */
<COMMENT> ::= <COMMENT_LINE> | <COMMENT_BLOCK>
<COMMENT_LINE> ::= "#" <ANY_WO_ESCAPES> <NEWLINE> 
<COMMENT_BLOCK> ::= "###" <ANY_WO_ESCAPES> "###"

/* molecules */
<ANY> ::= <ANY_SYM> | <ANY> <ANY_SYM>
<ANY_WO_ESCAPES> ::= <ANY_SYM_WO_ESCAPES> | <ANY_WO_ESCAPES> <ANY_SYM_WO_ESCAPES>

<SIGNABLE_DIGITS> ::= <SIGN> <DIGITS> | <DIGITS>
<DIGITS> ::= <DIGIT> | <DIGITS> <DIGIT>

/* atoms */
<IDENT_TAIL_SYM> ::= <DIGIT> | <IDENT_HEAD_SYM>
<IDENT_HEAD_SYM> ::= <LETTER> | <UNDERSCORE>

<ANY_SYM> ::= <ANY_SYM_WO_ESCAPES> | <ESCAPED_SYM>
<ANY_SYM_WO_ESCAPES> ::= <DIGIT> | <LETTER> | <SPECIAL>

<ESCAPED_SYM> ::= "\\" <SYM_TO_BE_ESCAPED>
<SYM_TO_BE_ESCAPED> ::= "n" | "r" | "t" | "\\" | "\""

<NEWLINE> ::= "\\n" 
<SIGN> ::= "+" | "-"
<UNDERSCORE> ::= "_"

<SPECIAL> ::= "!" | "#" | "@" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "," | "." | "/" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "]" | "^" | "`" | "{" | "}" | "|" | "~" | " " | "_" | "+" | "-" 
<LETTER> ::= <LCASE_LETTER> | <UCASE_LETTER>
<UCASE_LETTER> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<LCASE_LETTER> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<DIGIT> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<struct_head> 


struct Test {
	kajsdflksjd;
	kljalksdf;
	klasjdf;
}


struct 

<FOR_STMT> ::= "for" "(" <FOR_INIT> ";" <FOR_CONDITION> ";" <FOR_INCREMENT> ")" <STMT_BLOCK>
